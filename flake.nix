{
  description = "Elixir release using mixRelease and mix2nix";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs";
  };

  outputs =
    {
      self,
      nixpkgs,
    }:
    let
      name = "my-sensibo";
      version = "0.1.0";

      system = "x86_64-linux";
      # system = builtins.currentSystem;

      pkgs = import nixpkgs { inherit system; };

      lib = pkgs.lib;

      beamPackages = pkgs.beamPackages;

      # Import dependencies generated by `mix2nix`
      mixDeps = import ./deps.nix { inherit lib beamPackages; };

      myApp = beamPackages.mixRelease {
        pname = name;
        version = version;
        src = self;

        mixEnv = "prod";
        removeCookie = false;

        nativeBuildInputs = [
          beamPackages.hex
          beamPackages.rebar3
        ];

        mixNixDeps = mixDeps;
      };
    in
    {
      packages.${system} = {
        default = myApp;
        container = pkgs.dockerTools.buildImage {
          inherit name;
          tag = version;
          created = "now";
          # contents = self;
          copyToRoot = pkgs.buildEnv {
            name = "${name}-root";
            paths = [ myApp ];
            pathsToLink = [ "/bin" ];
          };
          # contents = packages.${system}.default;
          # config.Cmd = [ "${self}/bin/sensibo" ];
          config.Cmd = [ "/bin/sensibo" ];
          # config.Cmd = [ "${packages.${system}.default}/bin/sensibo" ];
        };
      };

      # devShells.${system}.default = pkgs.mkShell {
      #   buildInputs = [
      #     pkgs.elixir
      #     pkgs.beamPackages.hex
      #     pkgs.beamPackages.rebar3
      #     pkgs.git
      #     pkgs.inotify-tools
      #     pkgs.hello
      #     # pkgs.nodejs # only if using assets (e.g., Phoenix)
      #   ];

      #   shellHook = ''
      #     export MIX_ENV=prod
      #   '';
      # };

      nixosModules.default =
        {
          config,
          lib,
          pkgs,
          ...
        }:
        let
          cfg = config.services.${name};
          user = "my-sensibo";
          # dataDir = "/var/lib/ravensiris-web";
        in
        {
          options.services.${name} = {
            enable = lib.mkEnableOption "${name}";
            sensibo_apikey = lib.mkOption {
              type = lib.types.str;
              description = "SENSIBO_APIKEY";
            };
            influxdb_version = lib.mkOption {
              type = lib.types.str;
              default = "v2";
              description = "INFLUXDB_VERSION";
            };
            influxdb_host = lib.mkOption {
              type = lib.types.str;
              default = "127.0.0.1";
              description = "INFLUXDB_HOST";
            };
            influxdb_port = lib.mkOption {
              type = lib.types.int;
              default = 8086;
              description = "INFLUXDB_PORT";
            };
            influxdb_org = lib.mkOption {
              type = lib.types.str;
              default = "org";
              description = "INFLUXDB_ORG";
            };
            influxdb_bucket = lib.mkOption {
              type = lib.types.str;
              default = "sensibo";
              description = "INFLUXDB_BUCKET";
            };
            influxdb_bucket_token = lib.mkOption {
              type = lib.types.str;
              default = "default_token";
              description = "INFLUXDB_BUCKET_TOKEN";
            };
            influxdb_v1_database = lib.mkOption {
              type = lib.types.str;
              default = "sensibo";
              description = "INFLUXDB_DATABASE";
            };
            # influxdb_v1_username = lib.mkOption {
            #   type = lib.types.str;
            #   default = "default";
            #   description = "INFLUXDB_V1_USERNAME";
            # };
            # influxdb_v1_password = lib.mkOption {
            #   type = lib.types.str;
            #   default = "default";
            #   description = "INFLUXDB_V1_PASSWORD";
            # };
            cookie = lib.mkOption {
              type = lib.types.str;
              default = "bad_cookie";
              description = "COOKIE";
            };
          };
          config = lib.mkIf cfg.enable {
            assertions = [
              {
                assertion = cfg.sensibo_apikey != "";
                message = "A api key for sensibo is necessary";
              }
            ];

            systemd.services = {
              ${name} = {
                description = "My Sensibo";
                wants = [ "network-online.target" ];
                after = [
                  "network-online.target"
                  "influxdb2.service"
                ];
                requires = [ "influxdb2.service" ];
                wantedBy = [ "multi-user.target" ];
                script = ''
                  ${myApp}/bin/sensibo start
                '';
                serviceConfig = {
                  Type = "simple";
                  User = user;
                  Group = user;
                  DynamicUser = "yes";
                  Restart = "on-failure";
                  RestartSec = 5;
                };
                unitConfig = {
                  StartLimitBurst = 3;
                  StartLimitIntervalSec = 10;
                };
                # if we depend on bash...
                # path = [ pkgs.bash ];

                environment = {
                  SENSIBO_APIKEY = cfg.sensibo_apikey;
                  INFLUXDB_VERSION = cfg.influxdb_version;
                  INFLUXDB_V1_DATABASE = cfg.influxdb_v1_database;
                  # INFLUXDB_V1_USERNAME = cfg.influxdb_v1_username;
                  # INFLUXDB_V1_PASSWORD = cfg.influxdb_v1_password;
                  INFLUXDB_BUCKET = cfg.influxdb_bucket;
                  INFLUXDB_BUCKET_TOKEN = cfg.influxdb_bucket_token;
                  INFLUXDB_HOST = cfg.influxdb_host;
                  INFLUXDB_PORT = toString (cfg.influxdb_port);
                  INFLUXDB_ORG = cfg.influxdb_org;
                  #
                  # Disable Erlang's distributed features
                  # RELEASE_DISTRIBUTION = "none";
                  ERL_EPMD_ADDRESS = "127.0.0.1";
                  RELEASE_COOKIE = cfg.cookie;
                };
              };
            };
          };
        };
    };
}
